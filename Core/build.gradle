plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0'
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.18"
}

dependencies {
    paperweight.paperDevBundle("1.21.8-R0.1-SNAPSHOT")
    implementation project(':API')
    implementation project(':NMS')
    implementation project(':MC_1_21_4')
    implementation project(':MC_1_21_5')
    implementation project(':MC_1_21_7')
    implementation project(':MC_1_21_8')

    compileOnly 'io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT'
    compileOnly 'com.github.retrooper:packetevents-spigot:2.9.4'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'io.lumine:Mythic-Dist:5.6.0'
    compileOnly 'org.jetbrains:annotations:24.1.0'
    compileOnly name: 'nightcore-2.7.18', version: '2.7.18'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') { expand(project.parent.properties) }
    filesMatching('paper-plugin.yml') { expand(project.parent.properties) }
}

tasks.named('shadowJar') {
    archiveBaseName = rootProject.name
    archiveClassifier = ''
}

paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.getMOJANG_PRODUCTION()

tasks.named('assemble') { dependsOn tasks.named('reobfJar') }

tasks.named('shadowJar') { finalizedBy tasks.named('reobfJar') }

tasks.register('deliverJars', Copy) {
    dependsOn tasks.named('reobfJar'), tasks.named('shadowJar')
    from(layout.buildDirectory.dir('libs')) {
        include "${rootProject.name}-${version}.jar"
    }
    into("${rootDir}/target")
}

tasks.named('build') { finalizedBy tasks.named('deliverJars') }
